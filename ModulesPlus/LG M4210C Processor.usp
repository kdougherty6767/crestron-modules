/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: Crestron Electronics
System Name:
System Number:
Programmer: PAC
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#SYMBOL_NAME "LG M4210C Processor"
// #HINT ""
#DEFINE_CONSTANT CONTRAST   71
#DEFINE_CONSTANT BRIGHTNESS 72
#DEFINE_CONSTANT COLOR      73
#DEFINE_CONSTANT TINT       74
#DEFINE_CONSTANT SHARPNESS  75
#DEFINE_CONSTANT VOLUME			70

// #CATEGORY "" 
// #DEFAULT_NONVOLATILE
#DEFAULT_VOLATILE
#HELP_BEGIN
	This module is for use inside the LG M4210C module only.
#HELP_END
/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT Send, AirIn, ChannelGo, DigitalIn[12]; 
ANALOG_INPUT Address, ChannelIn; 
BUFFER_INPUT FromDevice$[512]; 

STRING_OUTPUT ToDevice$; 
ANALOG_OUTPUT Channel, AnalogOut[6]; 
/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
*******************************************************************************************/
INTEGER Semaphore, element, IDReceived, Storage[16], Value, Parameter;
STRING Tempstring$[255];
/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
Integer_Function Calculate(Integer MSB, Integer LSB)
	{
	if(MSB < 58)
		MSB = MSB - 48;
	else
		MSB = MSB - 55;
	if(LSB < 58)
		LSB = LSB - 48;
	else
		LSB = LSB - 55;
	Return(MSB * 16 + LSB);
	}
		
/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
PUSH DigitalIn
	{
	Element = getlastmodifiedarrayindex();
	}

Release DigitalIn
	{
	Element = 0;
	}

PUSH Send
	{
    if((Element < 7) && (Element > 0))
    	{
    	if(Storage[Element] < 100)
    		{
    		Storage[Element] = Storage[Element] + 1;
    		Switch(Element)
    			{
    			Case(1): { Makestring(ToDevice$, "kg %02X %02X\r", Address, Storage[Element]); }
    			Case(2): { Makestring(ToDevice$, "kh %02X %02X\r", Address, Storage[Element]); }
    			Case(3): { Makestring(ToDevice$, "ki %02X %02X\r", Address, Storage[Element]); }
    			Case(4): { Makestring(ToDevice$, "kj %02X %02X\r", Address, Storage[Element]); }
    			Case(5): { Makestring(ToDevice$, "kk %02X %02X\r", Address, Storage[Element]); }
    			Case(6): { Makestring(ToDevice$, "kf %02X %02X\r", Address, Storage[Element]); }
    			}
    		}
    	}
    else if((element > 6) && (element < 13))
    	{
    	if(Storage[element - 6] > 0)
    		{
    		Storage[element - 6] = storage[element - 6] - 1;
    		Switch(element - 6)
    			{
    			Case(1): { Makestring(ToDevice$, "kg %02X %02X\r", Address, Storage[Element - 6]); }
    			Case(2): { Makestring(ToDevice$, "kh %02X %02X\r", Address, Storage[Element - 6]); }
    			Case(3): { Makestring(ToDevice$, "ki %02X %02X\r", Address, Storage[Element - 6]); }
    			Case(4): { Makestring(ToDevice$, "kj %02X %02X\r", Address, Storage[Element - 6]); }
    			Case(5): { Makestring(ToDevice$, "kk %02X %02X\r", Address, Storage[Element - 6]); }
    			Case(6): { Makestring(ToDevice$, "kf %02X %02X\r", Address, Storage[Element - 6]); }
    			}
    		}
    	}		
	}
	
PUSH ChannelGo
	{
	if(AirIn = 1)
		Makestring(ToDevice$, "ma %02X %02X 0 0 00\r", Address, ChannelIn);
	else
		Makestring(ToDevice$, "ma %02X %02X 0 0 01\r", Address, ChannelIn);
	}

Change FromDevice$
	{
	if(Semaphore = 0)
		{
		Semaphore = 1;
		While((find("\x0D\x0A", FromDevice$) > 0) && (len(FromDevice$) > 8))
			{
			if(find("NG", FromDevice$) > 0)
				{
				Tempstring$ = Remove("\x0D\x0A", FromDevice$);
				tempstring$ = "";
				}
			else if(len(FromDevice$) > 21)
				{
				Tempstring$ = Remove("\x0D\x0A", FromDevice$);
				IDReceived = Calculate(Byte(Tempstring$, 3), Byte(Tempstring$, 4));
				if(IDReceived = Address)
					{
					Parameter = Byte(Tempstring$, 1);
					if(Parameter = 65)
						{
						Channel = CAlculate(Byte(Tempstring$, 8), Byte(Tempstring$, 9));
						}
					}
				}
			else if(len(FromDevice$) > 10)
				{
				Tempstring$ = Remove("\x0D\x0A", FromDevice$);
				IDReceived = Calculate(Byte(Tempstring$, 3), Byte(Tempstring$, 4));
				if(IDReceived = Address)
					{
					Parameter = Byte(Tempstring$, 1);
					Value = CAlculate(Byte(Tempstring$, 8), Byte(Tempstring$, 9));
					Switch(Parameter)
						{
						Case(Contrast):
							{
							AnalogOut[1] = Value;
							Storage[1] = Value;
							}
						Case(BRIGHTNESS):
							{
							AnalogOut[2] = VAlue;
							Storage[2] = Value;
							}
						Case(COLOR):
							{
							AnalogOut[3] = Value;
							Storage[3] = Value;
							}
						Case(TINT):
							{
							AnalogOut[4] = Value;
							Storage[4] = VAlue;
							}
						Case(SHARPNESS):
							{
							AnalogOut[5] = Value;
							Storage[5] = Value;
							}
						Case(VOLUME):
							{
							AnalogOut[6] = Value;
							Storage[6] = Value;
							}
						}
					}
				}
			}
		Semaphore = 0;
		}
	}
/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
Function Main()
	{
    Semaphore = 0;
    Element = 0;
	}
