#CATEGORY "0"
#SYMBOL_NAME "Qsys Pots Controller"

#USER_SIMPLSHARP_LIBRARY "QscQsys" 

#DEFINE_CONSTANT true 1
#DEFINE_CONSTANT false 0

Digital_Input Dial, Redial, Connect, Disconnect, AutoAnswerToggle, DndToggle, _skip_, KeyPadDelete, KeyPadClear, KeyPadStar, KeyPadPound, KeyPad[10], _skip_, _skip_, _skip_, _skip_, SelectRecentCall[5,5];
Analog_Input SelectRecentCallIndex;

Digital_Output Connected, Ringing, Dialing, IncomingCall, AutoAnswerStatus, DndStatus;
String_Output _skip_, _skip_, _skip_, _skip_, _skip_,_skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_,_skip_,_skip_, CurrentlyCalling, CallStatus, DialStringOut, RecentCallXSig, RecentCalls[5,5];

QsysPotsController pots;

String_Parameter _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_, _skip_,_skip_, _skip_, _skip_, _skip_, _skip_,_skip_,_skip_,_skip_,_skip_,_skip_, _skip_, _skip_,_skip_,_skip_,_skip_,_skip_, _skip_,_skip_,_skip_,_skip_,_skip_,_skip_,_skip_, CoreID[100], ComponentName[100];

Push Dial
{
    pots.Dial();
}

Push Redial
{
    pots.Redial();
}

Push Connect
{
    pots.Connect();
}

Push Disconnect
{
    pots.Disconnect();
}

Push AutoAnswerToggle
{
    pots.AutoAnswerToggle();
}

Push DndToggle
{
    pots.DndToggle();
}

Threadsafe Push KeyPad
{
    integer x;

    x = GetLastModifiedArrayIndex();

    pots.NumPad(ItoA(x - 1));
}

Push KeyPadDelete
{
    pots.NumPadDelete();
}

Push KeyPadClear
{
    pots.NumPadClear();
}

Push KeyPadStar
{
    pots.NumPad("*");
}

Push KeyPadPound
{
    pots.NumPad("#");
}

Threadsafe Push SelectRecentCall
{
    integer x;

    x = GetLastModifiedArrayIndex();

    pots.SelectRecentCall(x);
}

Threadsafe Change SelectRecentCallIndex
{
    integer x;

    while(x != SelectRecentCallIndex)
    {
        x = SelectRecentCallIndex;

        pots.SelectRecentCall(x);
    }
}

Callback Function NewOffHookEvent(string cName, integer value)
{
    if(value)
        Connected = true;
    else
        Connected = false;
}

Callback Function NewRingingEvent(string cName, integer value)
{
    if(value)
        Ringing = true;
    else
        Ringing = false;
}

Callback Function NewDialingEvent(string cName, integer value)
{
    if(value)
        Dialing = true;
    else
        Dialing = false;
}

Callback Function NewAutoAnswerEvent(string cName, integer value)
{
    if(value)
        AutoAnswerStatus = true;
    else
        AutoAnswerStatus = false;
}

Callback Function NewDndEvent(string cName, integer value)
{
    if(value)
        DndStatus = true;
    else
        DndStatus = false;
}

Callback Function NewDialStringEvent(string cName, string newDialString)
{
    DialStringOut = newDialString;
}

Callback Function NewCurrentlyCallingEvent(string cName, string newCurrentlyCalling)
{
    CurrentlyCalling = newCurrentlyCalling;
}

callback function NewCurrentCallStatusChange(string cName, string status)
{
    CallStatus = status;
}

callback function NewRecentCallsEvent(string cName, string call1, string call2, string call3, string call4, string call5)
{
    RecentCalls[1] = call1;
    RecentCalls[2] = call2;
    RecentCalls[3] = call3;
    RecentCalls[4] = call4;
    RecentCalls[5] = call5;
}

callback function NewRecentCallListEvent(string cName, string xsig)
{
        RecentCallXSig = xsig;
}

callback function NewIncomingCallEvent(string cName, integer value)
{
    if(value)
        IncomingCall = true;
    else
        IncomingCall = false;
}

Function Main()
{
    RegisterDelegate(pots, onOffHookEvent, NewOffHookEvent);
    RegisterDelegate(pots, onRingingEvent, NewRingingEvent);
    RegisterDelegate(pots, onDialingEvent, NewDialingEvent);
    RegisterDelegate(pots, onAutoAnswerEvent, NewAutoAnswerEvent);
    RegisterDelegate(pots, onDndEvent, NewDndEvent);
    RegisterDelegate(pots, onDialStringEvent, NewDialStringEvent);
    RegisterDelegate(pots, onCurrentlyCallingEvent, NewCurrentlyCallingEvent);
    RegisterDelegate(pots, onCurrentCallStatusChange, NewCurrentCallStatusChange);
    RegisterDelegate(pots, onRecentCallsEvent, NewRecentCallsEvent);
    RegisterDelegate(pots, onRecentCallListEvent, NewRecentCallListEvent);
    RegisterDelegate(pots, onIncomingCallEvent, NewIncomingCallEvent);
    pots.Initialize(CoreID, ComponentName);
}
